# 初めてのprocessing 第四回 クラスを使ってみる

## クラスとは
クラスとは、複数の変数と関数をまとめたものである。クラスではまとめられた変数をフィールド、関数をメソッドと呼ぶ。

processingでは標準で利用できるクラスがたくさん用意されている。  
例として、
* Stringクラス
* ArrayListクラス
* PImageクラス
* PGraphicsクラス  
などを挙げることができる。

## インスタンスについて
クラスを扱うには、インスタンスと呼ばれるクラスの実体を作る必要がある。  
なぜなら、クラスというのはあくまで型のような働きをするのであって、実際に値を持っているわけではないからである。

processingでの`"abc"`のような文字列リテラルは全て、StringクラスのインスタンスとしてふるまうためStringクラスを使用する際はあまり気にしていなかったと思う。

```java
void setup(){
  String str="abc";//Stringクラスの変数に"abc"のインスタンスを代入している
}
```
図1. Stringのメソッドが使用できる

インスタンスを作成するためには`new クラス名(引数)`のように書かなければならない。配列も一種のクラスである。
変数はインスタンスを共有しており、一方で変更されるともう一方も変化してしまう。

そのため、インスタンスを共有させないためには個別にnewする必要がある。
[第二回の図15](第二回.md#図15)のような現象が起こるのは二つの変数が同一のインスタンスを見ていたからである。

フィールドやメソッドにアクセスするには`変数名.フィールド名`や、`変数名.メソッド名(引数)`とする。この時、変数にインスタンスが代入されていないときはエラーが出力される。

しかし、インスタンスの情報を必要としない一部のフィールドや関数は、`クラス名.フィールド名`や、`クラス名.メソッド名(引数)`などでもアクセスできる。  
例. `String.format()`、`Integer.MAX_VALUE`

```java
void setup(){
  int max=Integer.MAX_VALUE;//intの最大値を表すフィールド
  String str=String.format("int型の最大値は%dです。",max);//文字列に値を挿入するメソッド
  println(str);
}
```
図2. インスタンス無しでアクセス可能なフィールドやメソッドの例

## Stringクラス
文字列を扱うクラスである。
文字リテラルはStringクラスのインスタンスとしてふるまう。

```java
void setup(){
  println("abc".charAt(0));//先頭の文字GET!
}
```
図2. Stringのメソッドが使用できる

文字列定数としてふるまう。定数であるため、中の文字列は変化しない。

```java
void setup(){
  String a="test1";
  String b="test2";
  String c=b;

  b+=a;//ここでbには、bとaが連結した内容の新たなインスタンスが代入されている

  println(b);//test2test1
  println(c);//test2
}
```
図3. 文字列は変化していない

## ArrayListクラス
ArrayListクラスは可変長配列を実装するクラスである。  
値を取得/登録にメソッドを使用する。

```java
void setup(){
  ArrayList<String> strList=new ArrayList<>();
  strList.add("test");//値の追加
  strList.add("test2");//値の追加
  strList.set(0,"test3");//0番目の要素の値の更新
  println(strList);//[test3, test2]
  println(strList.size());//要素数の表示
}
```
図4. ArrayListを使った要素の操作の例

動的に要素数が変化するため、後から要素を追加する必要のある実装をするときに非常に便利である。

## PImageクラス
processingで画像を利用するときに使用するクラスである。  
画像の読み込みは`loadImage`関数を用いて行う。  
指定したファイルはスケッチからの相対パス、もしくはdataフォルダの中にある必要がある。  
描画は`image`関数で行う。

## PGraphicsクラス
PImageクラスを基にしたクラス。
PImageの機能に加えて、processingの図形を描画する関数をメソッドとして使用できる。
インスタンスは`createGraphics`関数を使用し、描画はPGraphicsの`beginDraw`メソッドと`endDraw`メソッドの間で行う必要がある。

```java
void settings(){//ウィンドウのサイズ設定
  size(WID,HEI);
}

void setup(){//初期化
  mask=createGraphics(WID,HEI);//newしない
  img=createGraphics(WID,HEI);
  x=0;
  y=0;
}

final int WID=500;//ウィンドウ幅
final int HEI=500;//ウィンドウ高さ
final int BLOCK_SIZE=50;//背景の市松模様の大きさ
final int POINT_SIZE=10;//点のサイズ
int x,y;//背景を動かす変数
PGraphics mask;//マスク描画用
PGraphics img;//画像描画用

void draw(){
  drawBack();//背景描画
  drawMask();//マスク描画
  drawImg();//画像描画
  img.mask(mask);//マスクをする
  image(img,0,0);//描画
}

void drawBack(){//市松模様
  background(240);
  fill(0);
  for(int i=0;i<=(WID-x)/BLOCK_SIZE;i+=2){
    for(int j=0;j<=(WID-y)/BLOCK_SIZE;j++){
      rect((i+(j%2))*BLOCK_SIZE+x,j*BLOCK_SIZE+y,BLOCK_SIZE,BLOCK_SIZE);//四角を大量に描画
    }
  }
  //以下で背景を動かす
  x--;
  y--;
  if(-x>BLOCK_SIZE*2){
    x+=BLOCK_SIZE*2;
  }
  if(-y>BLOCK_SIZE*2){
    y+=BLOCK_SIZE*2;
  }
}

void drawMask(){//マスクの描画(マウスポインタを中心とした円)
  mask.beginDraw();
  mask.background(0);
  mask.fill(255);
  mask.ellipse(mouseX,mouseY,WID*2/3,HEI*2/3);
  mask.endDraw();
}

void drawImg(){//円運動のアニメーション
  img.beginDraw();
  img.background(255);
  img.line(WID/2,0,WID/2,HEI);//座標軸
  img.line(0,HEI/2,WID,HEI/2);
  img.fill(0);
  img.textAlign(RIGHT,TOP);
  img.textSize(50);
  img.text("O",WID/2,HEI/2);//原点O
  float px,py;
  int millis=millis();//時間で制御
  px=cos(PI*millis/2000)*WID/3;//x座標
  py=sin(PI*millis/2000)*WID/3;//y座標
  img.circle(WID/2+px,HEI/2+py,POINT_SIZE);//円運動する点
  img.line(WID/2+px,HEI/2+py,WID/2,HEI/2);
  img.circle(WID/2+px,HEI/2,POINT_SIZE);//x軸上の点
  img.circle(WID/2,HEI/2+py,POINT_SIZE);//y軸上の点
  img.endDraw();
}
```
図. PGraphicsを使用した例

## おまけ(プリミティブ型と参照型)
今更だが、変数は値を保持している。  
が、それは`int`などのプリミティブ型と呼ばれる型のお話。

今回説明したクラスは参照型である。参照型が変数に保持しているのはインスタンスの場所である。
そうなると困るのが等価演算子である。

どういうことかというと、processingの等価演算子の評価基準が、等価であるかではなく等値であるかであるためにインスタンスを比較したときに意図しない動作をすることがあるのである。

前述のとおりクラスの型の変数が持っているのはインスタンスの場所である。そのため、等価であっても別のインスタンスであった場合にfalseが帰ってくるのである。

```java
void setup(){
  String a=new String("test");//同じ内容の別のインスタンスを生成させる
  String b=new String("test");
  println(a==b);//別のインスタンス(場所が違う)であるためfalse
  String c,d;
  c=d="test";
  println(c==d);//同一インスタンス(場所が同じ)であるためtrue
}
```

そのために等価であるかどうかを調べるにはequalsメソッドを用いるとよい。

```java
void setup(){
  String a=new String("test");
  String b=new String("test");
  println(a.equals(b));//等価であるため別のインスタンスでもtrue
}
```

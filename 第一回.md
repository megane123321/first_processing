# 初めてのprocessing 第一回 変数と関数
## processingのダウンロード
まず、processingの[ダウンロードページ](https://processing.org/download)からprocessingをダウンロードする。

ダウンロード後、インストーラーを起動し、インストールを行う。
その際、選択肢が三つほど用意されている場所があるが、
`Typical`を選択する。  
インストールが完了したらアプリ一覧等からprocessingを起動できる。

三角のボタンを押すことでプログラムを実行することができる。実行されたときに出現する小さなウィンドウを使って自力でゲームを作れるようになること目標とする。

## processingでプログラミング
processingではプログラムは決められた状況に実行される。
例えば、`void setup(){}`はプログラムが起動された時に一度、後ろの波括弧の中を実行する。  

下のプログラムを実行してみよう。

<div id="図1"></div>

```java
void setup(){
  println("Hello world!");//コンソールに出力
}
```
図1. コンソールに`Hello world!`と出力する

実行するとコンソールに一回`Hello world!`と表示されたのが分かる。  

[表1](#表1)に`void setup(){}`のようなものの代表的な例をまとめる。

<div id="表1"></div>

表1. 実行されるまとまりの例
|実行されるまとまり|実行される状況|
|:-|:-|
|`void settings(){}`|プログラムの起動時に一度(`setup`より先)|
|`void setup(){}`|プログラムの起動時に一度(`setttings`より後)|
|`void draw()｛｝`|一秒間に60回[^1]|
|`void keyPressed(){}`|キーが押されたとき[^2]|
|`void keyReleased(){}`|キーが離されたとき[^2]|
|`void mousePressed(){}`|マウスボタンが押されたとき[^2]|
|`void mouseReleased(){}`|マウスボタンが離されたとき[^2]|

## 文
[図1](#図1)の二行目、`println("Hello world!");`と書いてある。最後の`;`は命令の区切り(**文**と呼ぶ)を表す重要な文字であるため、書き忘れるとエラーが出るため注意。

一行一文を基本としてプログラムは書かれている。

また、一つもしくは複数の文を`{}`で囲うと一つの文のようにふるまう。これを複合文と呼ぶ。

## 関数
実行されたときに何かしらの動作を行うものを**関数**と呼ぶ。
関数は[図1](#図1)中の`println("Hello world!")`のように`関数名(引数)`の形で記述される。中には引数を複数持つものや、引数を持たない関数も存在する。  
例えば、[図1](#図1)中の`println`は引数の値をコンソールに出力する関数である。  

<div id="図2"></div>


```java
void setup(){
  rect(25,25,50,50);//ウィンドウのx=25,y=25の場所から幅50、高さ50の四角形を描画する
}
```
図2. 引数を複数持つ関数の例 引数はコンマ区切りで渡す

## 変数と型
プログラムをするにあたっての最重要の要素である変数。その変数を一言で言い表すと、「**変わる数**」である。
[図3](#図3)を見てほしい。

<div id="図3"></div>

```java
void setup(){
  int x=5;//変数xの宣言(値は5)
  x+=7;//xに7を足す
  println(String.format("x=%d",x));//xの値をコンソールに出力
}
```
図3. 変数を使用したプログラム例

確かに`x`の値が12になっていることが確認できただろうか。
このように、計算を行うことで値を変えることのできる数を変数と呼ぶ。

変数の宣言は`型 変数名`のようにして行う。[図3](#図3)では`int`が型である。

型とは変数が扱く事のできる値の種類を表したものである。このように計算を行うことのできる変数だが値を何でもかんでも決められるわけではない。  

<div id="表2"></div>

表2. 代表的な型と扱える値

|型|入る値|値の例|
|:-|:-|:-|
|`int`|整数|`10`|
|`char`|一文字(整数)|`'a'`|
|`float`/`double`|小数|`0.3`|
|`String`|文字列|`"abc"`|
|`boolean`|bool値|`true`|

値は型が違う変数には代入できないことに注意したい。

<div id="図4"></div>

```java
void setup(){
  int x="str";//Type mismatch, "java.lang.String" does not match with "int"
}
```
図4. 型が違うため代入できない

変数に行える計算を[表3](#表3)に示す。

<div id="表3"></div>

表3. 変数に適用できる計算の例

|演算の種類|記号|備考|
|-|-|-|
|足し算|`+`|特例として文字列の連結も可能である。|
|引き算|`-`|
|掛け算|`*`|足し算/引き算よりも優先される。|
|割り算|`/`|足し算/引き算よりも優先される。|
|あまり|`%`|
|インクリメント|`++`|値を`1`増やす。変数の前と後ろのどちらに置くかで意味が少し変わる。|
|デクリメント|`--`|値を`1`減らす。変数の前と後ろのどちらに置くかで意味が少し変わる。|
|代入|`=`|右側の演算結果を左側の変数に代入する。等しいという意味はないことに注意。|
|複合代入|`+=`<br>`-=`<br>`*=`<br>`/=`<br>`%=`|右側の演算結果を左側の変数の値をそれぞれの計算方法で計算した結果を左側の変数に代入する。|

<div id="図5"></div>


```java
void setup(){//一番最初に実行
  size(500,150);//ウィンドウのサイズ設定
  x=0;//変数の初期化
}

int x;//変数の宣言

void draw(){//秒間60回実行
  background(255);//背景の色を白くする
  rect(x++,50,50,50);//x座標を増やしながら四角を描画
}

void keyPressed(){//キーが押されたとき
  x=0;//x座標を0に設定
}
```
図5. 変数を使用したプログラム例2

<div id="型の計算例"></div>

変数の計算の答えは精度の高い方へそろえられる。  
例えば、int型同士の計算だと答えもint型で、int型とfloat型だとfloat型で、float型とdouble型だとdouble型で答えが出てくる。

## 変数の見える範囲
実は変数には有効範囲がある。まとまりの中で宣言された変数はまとまりの外では使えない。

試しに[図5](#図5)のプログラムを少し変えてみる。

<div id="図6"></div>


```java
void setup(){
  size(500,150);
  int x=0;//まとまりの中で宣言された変数は
}

void draw(){
  background(255);
  rect(x++,50,50,50);//まとまりの外側からは使えない
}

void keyPressed(){
  x=0;//まとまりの外側からは使えない
}
```
図6. 図5のプログラムの変数`x`の宣言を`setup`内で行うように変更

どのまとまりでも使えるように一番外側で宣言された変数を特別に**グローバル変数**と呼ぶ。  
[図5](#図5)では`x`がグローバル変数だったためどのまとまりからでも使えたのだ。

## 定数
プログラムを書く際に数字をそのまま書くと何をしているのかが分かりにくくなってしまう。そこで定数という機能を使う。  
定数は変更ができない変数のことだ。変更を行おうとするとエラーが出る。  
`final 型 定数名`で記述される。基本的に大文字で書かれる。

<div id="図7"></div>


```java
void settings(){
  size(500,500);
}

void setup(){
  background(255);
  fill(0);//テキストの色を黒に設定
  line(0,450,500,450);//x軸
  line(250,0,250,500);//y軸
  textAlign(LEFT,TOP);
  textSize(25);
  text("O",250,450);
  x=-250;
  py=0;
}

int x;
float py;

void draw(){
  float y=0.005*x*x;
  line(x+249,-py+450,x+250,-y+450);
  py=y;
  x++;
}
```
図7. 定数を使わなかった場合

<div id="図8"></div>


```java
void settings(){
  size(WINDOW_WID,WINDOW_HEI);
}

void setup(){
  background(255);
  fill(0);//テキストの色を黒に設定
  line(0,-O_Y+WINDOW_HEI,WINDOW_WID,-O_Y+WINDOW_HEI);//x軸
  line(O_X,0,O_X,WINDOW_HEI);//y軸
  textAlign(LEFT,TOP);
  textSize(TEXT_SIZE);
  text("O",O_X,-O_Y+WINDOW_HEI);
  x=-O_X;
  py=0;
}

final int WINDOW_WID=500;//ウィンドウのサイズ
final int WINDOW_HEI=500;
final int O_X=250;//原点の位置
final int O_Y=50;
final int TEXT_SIZE=25;
final float FUNC_A=0.005;//関数の比例定数
int x;
float py;

void draw(){
  float y=FUNC_A*x*x;
  line(x-1+O_X,-py-O_Y+WINDOW_HEI,x+O_X,-y-O_Y+WINDOW_HEI);//ひとつ前の座標(x-1,py)と(x,y)を線で結ぶ
  py=y;
  x++;
}
```
図8. 定数を使った場合

定数を使うと使わない方に比べて読みづらいものの、値の意図が分かりやすくなっている。特に複数人で開発する際は分かりやすくするため、必要に応じて定数を使うように心がけるとよいだろう。

## おまけ(int型同士の割り算)
int型同士で計算を行うと、[答えはint型になる](#型の計算例)と説明したが、int型同士で割り算をして割り切れなかったら答えはどうなるだろうか?  
int型同士で割り算を行うと小数点以下が切り捨てられることになる。

<div id="図9"></div>


```java
void setup(){
  int a=8;
  int b=3;
  println(a/b);//2
}
```
図9. 小数点以下は切り捨て

そのため、答えを小数で得るには型を変更する必要がある。  
そこで使えるのがキャストだ。

<div id="図10"></div>


```java
void setup(){
  int a=8;
  int b=3;
  println((float)a/b);//2.6666667
}
```
図10. `a`をfloat型にキャストしてから割り算

[図10](#図10)のように変数の前に`(型)`と書くことで任意の型に変更できる。
キャストを使うと値を可能であれば他の型に変換することができるが変数の型自体は変わらないため注意。

<div id="図11"></div>

```java
void setup(){
  float a=1.5;
  println((int)a);
  println(a);
}
```
図11. 一度キャストしても変数の型はそのまま

<div id="図12"></div>


```java
void setup(){
  String str="Hello world!";
  prinln((int)str);//Cannot cast from String to int
}
```
図12. どう見たって無理なものは無理

## おまけ(画面の描画に使える関数たち)
画面の描画を行える関数を[表4](#表4)に示す。processingではx+方向が右、y+方向が下であることに注意すること。

<div id="表4"></div>

表4. 画面の描画を行う関数の例
|関数名(引数)|例|効果|
|-|-|-|
|`fill (gray)`|`fill (255)`|`0`~`255`の値で塗りつぶしの色をグレースケールで設定する。|
|`fill (r,g,b)`|`fill (255,200,200)`|RGB(それぞれ`0~255`で指定)で塗りつぶしの色を設定する。|
|`background (gray)`|`background (255)`|`0`~`255`の値で背景の色をグレースケールで塗りつぶすする。|
|`background (r,g,b)`|`background (200,255,200)`|RGB(それぞれ`0~255`で指定)で背景を塗りつぶす。|
|`rect (x,y,dx,dy)`|`rect (50,50,100,100)`|`(x1,y1)`の点を左上の頂点に持つ幅`dx`、高さ`dy`の四角形を描く。|
|`circle (x,y,d)`|`circle (100,100,150)`|`(x,y)`を中心とした直径`d`の円を描く。|
|`ellipse (x,y,dx,dy)`|`ellipse (100,100,100,150)`|`(x,y)`を中心(焦点ではない)に幅`dx`高さ`dy`の楕円を描く。|
|`line (x1,y1,x2,y2)`|`line (50,50,100,150)`|始点が`(x1,y1)`、終点が`(x2,y2)`の線を引く。|
|`text (str,x,y)`|`text ("Hello World!",0,0)`|`(x,y)`を起点に文字列`str`を描画する|
|`textSize (size)`|`textSize (50)`|画面に描画するテキストのサイズを設定する|
|`textAlign (pos1,pos2)`|`textAlign (CENTER,CENTER)`|テキストの描画の起点をずらす|

## 演習問題
問1. プログラムを起動してからの経過時間(単位:秒)をウィンドウの中央に表示するプログラムを作成せよ。ウィンドウサイズは500*500、背景は白、テキストサイズは100、テキストの色は黒とする。

<details>
<summary>
回答例
</summary>

```java
void setup(){
    size(500,500);
    count=0;
}

int count;

void draw(){
    background(255);
    textAlign(CENTER,CENTER);
    textSize(100);
    fill(0);
    text(count/60,width/2,height/2);
    count++;
}
```

</details>

問2. キーが押されたとき、押されたキーの文字をコンソールに出力、最後にキーが押されてからの時間(単位:秒、起動時は0)をウィンドウの中央に表示するプログラムを作成せよ。ウィンドウサイズは500*500、背景は白、テキストサイズは100、テキストの色は黒とする。   
ヒント: 最後に操作されたキーの情報は`key`変数、ウィンドウの幅は`width`変数、ウィンドウの高さは`height`変数(全て宣言する必要がない)が持っている。

<details>
<summary>
回答例
</summary>

```java
void setup(){
    size(500,500);
    count=0;
}

int count;

void draw(){
    background(255);
    textAlign(CENTER,CENTER);
    textSize(100);
    fill(0);
    text(count/60,width/2,height/2);
    count++;
}

void keyPressed(){
  println(key);
  count=0;
}
```

</details>

<div id="問2"></div>

問2. $y=(0.03x)^3$のグラフを描画せよ。原点はウィンドウの中央で、ウィンドウサイズは500*500、背景は白、テキストサイズは25、テキストの色は黒とする。 [図8](#図8)を参考にすること。  
ヒント: $a^n$を計算するには`pow(a,n)`の関数を用いるとよい。

<details>
<summary>
回答例
</summary>

```java
void settings(){
  size(WINDOW_WID,WINDOW_HEI);
}

void setup(){
  background(255);
  fill(0);
  line(0,-O_Y+WINDOW_HEI,WINDOW_WID,-O_Y+WINDOW_HEI);//x軸
  line(O_X,0,O_X,WINDOW_HEI);//y軸
  textAlign(LEFT,TOP);
  textSize(TEXT_SIZE);
  text("O",O_X,-O_Y+WINDOW_HEI);
  x=-O_X;
  py=0;
}

final int WINDOW_WID=500;//ウィンドウのサイズ
final int WINDOW_HEI=500;
final int O_X=250;//原点の位置
final int O_Y=250;
final int TEXT_SIZE=25;
int x;
float py;

void draw(){
  float y=pow(0.03*x,3);
  line(x-1+O_X,-py-O_Y+WINDOW_HEI,x+O_X,-y-O_Y+WINDOW_HEI);
  py=y;
  x++;
}
```

</details>

[^1]: `noLoop`関数や`frameRate`関数などの関数が実行されていない場合

[^2]: `void draw(){}`が書かれている必要がある
